PROGRAM remotemotionstart

%COMMENT = 'Remote Motion Abort'
%NOLOCKGROUP
%NOBUSYLAMP

VAR
	response_file :FILE
	STATUS: INTEGER
	high_speed: STRING[127]
	hs_flag: BOOLEAN
	
%INCLUDE ../util/generic_util
%INCLUDE ../core/core_motion_start

BEGIN
	
	OPEN FILE response_file ('RW', 'RD:RESPONSE.HTM')
	STATUS = IO_STATUS(response_file)
	IF (STATUS <> 0) THEN
		RETURN
	ENDIF
	
	-- terminate motion TP task
	ABORT_TASK('motion_backend', TRUE, TRUE, STATUS)
	ABORT_TASK('hs_motion_backend', TRUE, TRUE, STATUS)
	DELAY(1000) -- yield to be able to restart TP routine
	-- start new motion TP task
	
	IF(UNINIT(high_speed)) THEN
		hs_flag = FALSE
	ELSE
		hs_flag = STR_TO_BOOL(high_speed)
	ENDIF
	
	SET_INT_REG(8, 0, STATUS)
	STATUS = CORE_MOTION_START(hs_flag)
	
	IF (STATUS <> 0 ) THEN
		WRITE response_file ( '{"result":"failed", "reason":"Could not start motion_backend TP routine"}') 
	ELSE
		WRITE response_file ( '{"result":"success"}')
	ENDIF
	
	CLOSE FILE response_file
END remotemotionstart